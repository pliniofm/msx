10 '*******************************
20 '
30 ' CREATED BY NORBERTO TSOLEFSKI
40 '
50 '*******************************
60 CLEAR200,&HC000:DIMF$(20),N(20)
70 ONKEYGOSUB970,1240,1710,60,740,2590,2820
80 FORI=1TO7:KEY(I)ON:NEXT
90 DEFINTA-D
100 SCREEN0:WIDTH36:COLOR1,3
110 KEYOFF:RESTORE1910
120 PRINTSTRING$(36,203)
130 PRINT:PRINT"  ESTE PROGRAMA TRABALHA COM ELEMEN-TOS QUIMICOS DA SEGUINTE FORMA:"
140 PRINT:PRINT"F1=DISTRIBUI€°O EM NIVEIS ENERGETI- COS"
150 PRINT"F2=DIAGRAMA DE LINUS PAULING"
160 PRINT:PRINT"F3=FICHA COM CARACTERISTICAS"
170 PRINT:PRINT"F4=RETORNA A ESTA TELA"
180 PRINT:PRINT"F5=DISTRIBUI€°O EM SUBNIVEIS ENERGE-TICOS"
190 PRINT"F6=LOCALIZA€°O NA TABELA"
200 PRINT:PRINTSTRING$(36,204)
210 LOCATE0,20
220 INPUT"  QUAL O NOME DO ELEMENTO OU SEU No ATOMICO";N$
230 IFVAL(N$)=0THEN320ELSEIFVAL(N$)>105THENN$="105"
240 '
250 '     PROCURA NA TABELA
260 '
270 FORI=1TOVAL(N$)
280 READE$,S$,M$
290 NEXT
300 Z=I-1
310 GOTO 370
320 IFLEN(N$)=2THENMID$(N$,2,1)=CHR$(ASC(RIGHT$(N$,1))+32)
325 FORI=1TO105
330 READE$,S$,M$
340 IFE$=N$ORS$=N$THENZ=I:I=105:GOTO360
350 NEXT
360 A$=N$
370 S=Z:RESTORE 2430
380 '
390 '     CALCULA NUM. ELETRONS
400 '
410 FORE=1TO19
420 READF$(E):Y$=MID$(F$(E),2)
430 IFY$="s"THENW=2
440 IFY$="p"THENW=6
450 IFY$="d"THENW=10
460 IFY$="f"THENW=14
470 S=S-W:IFS<0THENN(E)=S+W
480 IFS=0THENN(E)=W
490 IFS>0THENN(E)=W:NEXT
500 FORX=1TOE
510 V=VAL(F$(X))
520 VI(V)=VI(V)+N(X)
530 NA=NA-(V>NA)
540 NEXT:K=NA
550 A$=MID$(F$(E),2,1):IFA$="s"ANDN(E)=1THENG$="Alcalinos"
560 IFA$="s"ANDN(E)=2THENG$="Alcalino-terrosos"
570 IFA$="p"ANDN(E)=1THENG$="fam¡lia:boro"
580 IFA$="p"ANDN(E)=2THENG$="fam¡lia:carbono"
590 IFA$="p"ANDN(E)=3THENG$="fam¡lia:Nitrogˆnio"
600 IFA$="p"ANDN(E)=4THENG$="fam¡lia:Calcogˆnios"
610 IFA$="p"ANDN(E)=5THENG$="fam¡lia:Halogenios"
620 A$=MID$(F$(E),1,2):IFA$="4f"ANDN(E)<14THENG$="fam¡lia:lantanidios"
630 IFA$="5f"ANDN(E)<14THENG$="fam¡lia:Act¡nios"
640 IFMID$(F$(E),2,1)="d"ANDN(E)<10THENG$="-De transi‡±o"
650 IF Z=70 OR Z=71 THEN G$="fam¡lia: lantan¡dios"
660 IF Z=102 OR Z=103 THEN G$="fam¡lia: Actin¡dios"
670 IF VI(K)=8 THEN G$="Gases Nobres"
680 IF E$="HELIO" THEN G$="Gases Nobres"
690 GOTO1710
700 '
710 '   DISTRIBUI-128-AO EM SUBNIVEIS
720 '   ENERGETICOS
730 '
740 COLOR1,15:SCREEN2
750 FORI=1TO5:KEY(I)ON:NEXT
760 CLOSE
770 OPEN"GRP:"AS#1:XX=10:YY=80
780 LINE(5,50)-(250,180),1,BF
790 COLOR15
800 FORX=1TOE
810 PSET(XX,YY),1:PRINT#1,F$(X)
820 PSET(XX+7,YY-4),1:PRINT#1,N(X);
830 LINE(XX+28,YY+10)-(XX+40,YY-10)
840 XX=XX+40
850 IFXX>245THENXX=10:YY=YY+40
860 NEXT:COLOR1
870 LINE(5,5)-(250,35),7,BF
880 PSET(10,10),7:PRINT#1," Distribui‡±o em subn¡veis e-    nergeticos de acordo com o     Diagrama de Pauling(tecle F2)"
890 GOTO 890
900 FORX=1TOK
910 PRINTVI(X):NEXT
920 FORX=1TOI:PRINTF$(X),N(X):NEXT
930 GOTO1710
940 '
950 'DESENHA OS ELETRONS NAS CAMADAS
960 '
970 COLOR1,15:SCREEN2:C=20
980 FORI=1TO5:KEY(I)ON:NEXT
990 R=RND(-TIME)
1000 CLOSE
1010 OPEN"GRP:"AS#1
1020 FORX=1TOK:PSET(208,(X+4)*10),15:PRINT#1,USING"&=##";CHR$(74+X);VI(X)
1030 P!=6.2856/VI(X)
1040 FOR J=0 TO 6.2856 STEP P!
1050 XC=112+C*SIN(J):YC=96+C*COS(J)
1060 CIRCLE(XC,YC),3,1
1070 PAINT(XC,YC),1
1080 NEXT
1090 CIRCLE(112,96),C,1
1100 C=C+12:NEXT
1110 PRESET(10,180):COLOR 4
1120 PRINT#1,TAB(3);E$,"Z=";Z
1130 CIRCLE(112,96),7,1
1140 FORX=1TO50
1150 A=RND(1)*6:B=RND(1)*6
1160 C=RND(1)*2:IF C=1 THEN B=-B
1170 D=RND(1)*2:IF D=1 THEN A=-A
1180 PSET(112+A,96+B),1
1190 NEXT
1200 GOTO1200
1210 '
1220 'DIAGRAMA DE LINUS PAULING
1230 '
1240 COLOR1,7,4:SCREEN2
1250 FORI=1TO5:KEY(I)ON:NEXT
1260 CLOSE
1270 OPEN"GRP:"AS#1
1280 FORI=1TO7
1290 PRESET(50,20+20*I):I$=STR$(I)
1300 PRINT#1,I$+"s"
1310 NEXT
1320 FORI=2TO6
1330 PRESET(85,20+20*I):I$=STR$(I)
1340 PRINT#1,I$+"p"
1350 NEXT
1360 FORI=3TO6
1370 PRESET(120,20+20*I):I$=STR$(I)
1380 PRINT#1,I$+"d"
1390 NEXT
1400 FORI=4TO5
1410 PRESET(155,20+20*I):I$=STR$(I)
1420 PRINT#1,I$+"f"
1430 NEXT
1440 FORI=1TO7
1450 LINE(42,34+20*I)-(52,28+20*I)
1460 NEXT
1470 FORI=1TO7
1480 LINE(76,18+20*I)-(90,10+20*I):I$=STR$(I)
1490 NEXT
1500 FORI=2TO7
1510 LINE(76+35,18+20*I)-(90+35,10+20*I)
1520 NEXT
1530 FORI=3TO6
1540 LINE(76+70,18+20*I)-(90+70,10+20*I)
1550 NEXT
1560 FORI=4TO5
1570 LINE(181,18+20*I)-(195,10+20*I)
1580 NEXT
1590 FORI=1TO7
1600 PRESET(42,34+20*I)
1610 LINE-(45,27+20*I)
1620 PRESET(42,34+20*I)
1630 LINE-(46,35+20*I)
1640 NEXT
1650 LINE(10,5)-(240,20),3,BF
1660 PRESET(3,10):PRINT#1,"   DIAGRAMA DE LINUS PAULING"
1670 GOTO1670
1680 '
1690 ' FICHA DOS ELEMENTOS
1700 '
1710 COLOR1,15,4:SCREEN2,1
1720 FORI=1TO5:KEY(I)ON:NEXT
1730 LINE(30,30)-(220,160),7,BF
1740 X$=S$:GOSUB2500
1750 PUTSPRITE1,(40,40),1,1
1760 PUTSPRITE2,(55,40),1,2
1770 CLOSE
1780 OPEN"GRP:"AS#1:C=7
1790 PSET(50,60),C:COLOR4:PRINT#1,"ELEMENTO:";E$:COLOR1
1800 PSET(57,72),C:PRINT#1,"-NUM. ATOMICO:";Z
1810 PSET(57,82),C:PRINT#1,"-PESO ATOMICO: ";M$
1820 NE=INT(VAL(M$))-Z
1830 PSET(57,92),C:PRINT#1,"-NEUTRONS....:";NE
1840 PSET(57,102),C:PRINT#1,"-CAMADAS.....:";K
1850 IFVI(K)<4THENV$=" +"+STR$(VI(K))
1860 IFVI(K)>4THENV$=" -"+STR$(8-VI(K)):IFVI(K)=0THENV$=" 0"
1870 IFVI(K)=4THENPSET(57,112),C:PRINT#1,"-SEMICONDUTOR":GOTO1890
1880 PSET(57,112),C:PRINT#1,"-VALENCIA....:";V$
1890 COLOR15:PSET(57,122),C:PRINT#1,G$:I=0:T=0
1900 GOTO 1900
1910 DATA HIDROGENIO,H,1,HELIO,He,4
1920 DATA LITIO,Li,6.94,BERILIO,Be,9.01
1930 DATA BORO,B,10.8,CARBONO,C,12
1940 DATA NITROGENIO,N,14,OXIGENIO,O,16
1950 DATA FLUOR,F,19,NEONIO,Ne,20.2
1960 DATA SODIO,Na,23,MAGNESIO,Mg,24.3
1970 DATA ALUMINIO,Al,27,SILICIO,Si,28.1
1980 DATA FOSFORO,P,31,ENXOFRE,S,32.1
1990 DATA CLORO,Cl,35.5,ARGONIO,Ar,39.9
2000 DATA POTASSIO,K,39.1,CALCIO,Ca,40.1
2010 DATA ESCANDIO,Sc,45,TITANIO,Ti,47.9
2020 DATA VANADIO,V,50.9,CROMIO,Cr,52
2030 DATA MANGANES,Mn,54.9,FERRO,Fe,55.8
2040 DATA COBALTO,Co,58.9,NIQUEL,Ni,58.7
2050 DATA COBRE,Cu,63.5,ZINCO,Zn,65.4
2060 DATA GALIO,Ga,69.7,GERMANIO,Ge,72.6
2070 DATA ARSENIO,As,74.9,SELENIO,Se,79
2080 DATA BROMO,Br,79.9,CRIPTONIO,Kr,83.8
2090 DATA RUBIDIO,Rb,85.5,ESTRONCIO,Sr,87.6
2100 DATA ITRIO,Y,88.9,ZIRCONIO,Zr,91.2
2110 DATA NIOBIO,Nb,92.9,MOLIBDENIO,Mo,95.9
2120 DATA TECNECIO,Tc,99,RUTENIO,Ru,101
2130 DATA RODIO,Rh,103,PALADIO,Pd,106
2140 DATA PRATA,Ag,108,CADMIO,Cd,112
2150 DATA INDIO,In,115,ESTANHO,Sn,119
2160 DATA ANTIMONIO,Sb,122,TELURIO,Te,128
2170 DATA IODO,I,127,XENONIO,Xe,131
2180 DATA CESIO,Cs,133,BARIO,Ba,137
2190 DATA LANTANIO,La,139,CERIO,Ce,140
2200 DATA PRASEODIMIO,Pr,141,NEODIMIO,Nd,144
2210 DATA PROMECIO,Pm,147,SAMARIO,Sm,150
2220 DATA EUROPIO,Eu,152,GADOLINIO,Gd,157
2230 DATA TERBIO,Tb,159,DISPROSIO,Dy,163
2240 DATA HOLMIO,Ho,165,ERBIO,Er,167
2250 DATA TULIO,Tm,169,ITERBIO,Yb,173
2260 DATA LUTECIO,Lu,175,HAFNIO,Hf,178
2270 DATA TANTALIO,Ta,181,TUNGSTENIO,W,184
2280 DATA RENIO,Re,186,OSMIO,Os,190
2290 DATA IRIDIO,Ir,192,PLATINA,Pt,195
2300 DATA OURO,Au,197,MERCURIO,Hg,201
2310 DATA TALIO,Ti,204,CHUMBO,Pb,207
2320 DATA BISMUTO,Bi,209,POLONIO,Po,210
2330 DATA ASTATO,At,210,RADONIO,Rn,222
2340 DATA FRANCIO,Fr,223,RADIO,Ra,226
2350 DATA ACTINIO,Ac,227,TORIO,Th,232
2360 DATA PROTACTINIO,Pa,231,URANIO,U,238
2370 DATA NEPTUNIO,Np,237,PLUTONIO,Pu,242
2380 DATA AMERICIO,Am,243,CURIO,Cm,247
2390 DATA BERQUELIO,Bk,247,CALIFORNIO,Cf,251
2400 DATA EINSTEINIO,Es,254,FERMIO,Fm,253
2410 DATA MENDELEVIO,Md,256,NOBELIO,No,254
2420 DATA LAWRENCIO,Lr,257,KURCHATOVIO,Ku,272
2425 DATA HAHNIO,Ha,268
2430 DATA 1s,2s,2p,3s,3p,4s,3d,4p,5s,4d,5p,6s,4f,5d,6p,7s,5f,6d,7p
2440 '
2450 '   CRIA SPRITE AMPLIADOS COM
2460 '   SIMBOLOS DOS ELEMENTOS
2470 '
2480 X$=S$
2490 SCREEN1,1
2500 DEFFNS=PEEK(A*8+&H1BBF+S)
2510 FORL=1TOLEN(X$)
2520 A=ASC(MID$(X$,L,1))
2530 FORS=0TO7
2540 Q$=Q$+CHR$(FNS)
2550 NEXT
2560 SPRITE$(L)=Q$:Q$=""
2570 NEXT
2580 RETURN
2590 '
2600 '   TABELA PERIODICA
2610 '
2620 COLOR14,4:SCREEN2,0
2630 FORX=1TO6:KEY(X)ON:NEXT
2640 CLOSE
2650 OPEN"GRP:"AS#1
2660 LINE(8*7,8*8)-(21*8+7,12*8+7),,BF:LINE(8*7,13*8)-(8*11+7,8*13+7),,BF:LINE(10*8,15*8)-(24*8+7,16*8+7),12,BF:LINE(9*8,12*8)-STEP(8,15),4,BF
2670 LINE(8*7,8*7)-(8*7+7,8*7+7),12,BF:LINE(24*8,7*8)-(24*8+7,12*8+7),11,BF:LINE(20*8,8*8)-(23*8+7,8*10+7),8,BF:LINE(23*8,11*8)-(23*8+7,12*8+7),8,BF
2680 FORX=0TO2:LINE((20+X)*8,(9+X)*8)-STEP(7,15),3,BF:NEXT:LINE(19*8,8*8)-(19*8+7,8*8+7),3,BF:LINE(9*8,8*8)-(18*8+7,9*8+7),4,BF
2690 FORX=7TO24:LINE(X*8,7*8)-STEP(0,80),4:NEXT:FORX=7TO16:LINE(7*8,X*8)-STEP(18*8,0),4:NEXT
2700 DATA14,Metais,3,Semimetais,8,NFIELDo-metais,11,Gases nobres
2710 RESTORE2700:COLOR1:FORX=0TO3:READI,A$:LINE(15*8,(18+X)*8)-STEP(6,6),I,BF:PSET(17*8,(18+X)*8),4:PRINT#1,A$:NEXT
2720 PSET(64,10),4:PRINT#1,"Tabela peri¢dica":DRAW"BM116,80U2R16D2"
2730 PSET(8*3,18*8):PRINT#1,E$:PSET(8*3,19*8):PRINT#1,"Z=";Z:PSET(8*3,20*8):PRINT#1,"Periodo";K:PSET(8*3,8*8),4
2740 A$=RIGHT$(F$(E),1):IFA$="s"THENX=N(E)-1:Y=K-1:PRINT#1,USING"#A";N(E):IFZ=2THENX=17:LINE(8*3,8*8)-STEP(16,8),4,BF:PSET(8*3,8*8),4:PRINT#1,"8A"
2750 IFA$="f"THENX=N(E)+2:Y=K+2
2760 IFA$="p"THENX=N(E)+11:Y=K-1:PRINT#1,USING"#A";N(E)+2
2770 C=0:IFA$="d"THENI=N(E):X=I+1:Y=K-1:C=1:I=I+2:IFI>10THENI=I-10ELSEIFI>8THENI=8
2775 IFC=1THENPRINT#1,USING"#B";I
2780 IFZ=71ORZ=103THENX=17:Y=K+2
2790 SPRITE$(0)=STRING$(7,254)+CHR$(0):I=0:C=0
2800 PUTSPRITE0,(1+(X+7)*8,(Y+7)*8),C,0
2810 I=(I+1)AND63:C=(I>32)*-1:GOTO2800
2820 RUN "MENU.BAS"
